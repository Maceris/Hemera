# Hemera grammar

# Tokens

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
    | "H" | "I" | "J" | "K" | "L" | "M" | "N"
    | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
    | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
    | "c" | "d" | "e" | "f" | "g" | "h" | "i"
    | "j" | "k" | "l" | "m" | "n" | "o" | "p"
    | "q" | "r" | "s" | "t" | "u" | "v" | "w"
    | "x" | "y" | "z" ;

digit_decimal = "0" | digit_decimal_nonzero ;
digit_decimal_nonzero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

digit_hex = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
    | "a" | "A" | "b" | "B" | "c" | "C" | "d" | "D" | "e" | "E" | "f" | "F" ;
digit_octal = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" ;
digit_binary = "0" | "1" ;

identifier = letter , { letter | digit_decimal | "_" } ;

S = { " " | "\n" | "\t" | "\r" | "\f" | "\b" } ;

any_character = ? all characters ? ;
illegal_string_characters = "\x01" |"\x02" |"\x03" |"\x04" |"\x05"
    |"\x06" |"\x07" |"\x08" |"\x09" |"\x0b"
    |"\x0c" |"\x0d" |"\x0e" |"\x0f" |"\x10"
    |"\x11" |"\x12" |"\x13" |"\x14" |"\x15"
    |"\x16" |"\x17" |"\x18" |"\x19" |"\x1a"
    |"\x1b" |"\x1c" |"\x1d" |"\x1e" |"\x1f"
    |"\x7f" | "\n"
    ;

annotation = "@" , ( letter | digit_decimal | "_" )+ ;
directive = "#" , ( letter | digit_decimal | "_" )+ ;

comment_block = "/*" , { comment_block_body } , "*/" ;
comment_block_body = comment_block_body_not_nested
    | comment_block
    ;
comment_block_body_not_nested = { any_character - ( "*" | "/" ) }
    | ("/" , any_character - "*" , comment_block_body_not_nested)
    | ("*" , any_character - "/" , comment_block_body_not_nested)
    ;
comment_line = "//" , { any_character - "\n" } , "\n" ;

comment_any = comment_block | comment_line ;

literal_string = '"' , { any_character - ( "\" | '"' | illegal_string_characters ) | ( "\" , any_character - "n" ) } , '"' ;
literal_char = "'" , { any_character - ( "\" | "'" | illegal_string_characters ) | ("\" , any_character - ( "n" | illegal_string_characters ) ) } , "'" ;

literal_integer = integer_decimal
    | integer_hex
    | integer_binary
    | integer_octal
    ;

integer_decimal = digit_decimal_nonzero , { { "_" } , digit_decimal } ;
integer_hex = "0x" , digit_hex, { { "_" } , digit_hex } ;
integer_binary = "0b" , digit_binary, { { "_" } , digit_binary } ;
integer_octal = "0o" , digit_octal, { { "_" } , digit_octal } ;

literal_floating_point = ( "0" | integer_decimal ) , [ float_decimal ] , [ float_exponent ] ;
float_decimal = "." , integer_decimal ;
float_exponent = ( "e" | "E" ) , [ "-" ] , integer_decimal ;

pipe_reorder_identifier = "$" , integer_decimal ;

KEYWORD_alias = "alias" ;
KEYWORD_all = "all" ;
KEYWORD_as = "as" ;
KEYWORD_auto_cast = "auto_cast" ;
KEYWORD_bit_cast = "bit_cast" ;
KEYWORD_break = "break" ;
KEYWORD_case = "case" ;
KEYWORD_cast = "cast" ;
KEYWORD_continue = "continue" ;
KEYWORD_defer = "defer" ;
KEYWORD_distinct = "distinct" ;
KEYWORD_else = "else" ;
KEYWORD_enum = "enum" ;
KEYWORD_false = "false" ;
KEYWORD_fn = "fn" ;
KEYWORD_for = "for" ;
KEYWORD_from = "from" ;
KEYWORD_if = "if" ;
KEYWORD_import = "import" ;
KEYWORD_in = "in" ;
KEYWORD_is_none = "is_none" ;
KEYWORD_is_some = "is_some" ;
KEYWORD_loop = "loop" ;
KEYWORD_match = "match" ;
KEYWORD_mut = "mut" ;
KEYWORD_null = "null" ;
KEYWORD_or_break = "or_break" ;
KEYWORD_or_continue = "or_continue" ;
KEYWORD_or_else = "or_else" ;
KEYWORD_or_return = "or_return" ;
KEYWORD_package = "package" ;
KEYWORD_push_context = "push_context" ;
KEYWORD_return = "return" ;
KEYWORD_struct = "struct" ;
KEYWORD_switch = "switch" ;
KEYWORD_true = "true" ;
KEYWORD_union = "union" ;
KEYWORD_using = "using" ;
KEYWORD_void = "void" ;
KEYWORD_while = "while" ;
KEYWORD_with = "with" ;

OPERATOR_and = "&&" ;
OPERATOR_assign = "=" ;
OPERATOR_assign_add = "+=" ;
OPERATOR_assign_and = "&&=" ;
OPERATOR_assign_bitwise_and = "&=" ;
OPERATOR_assign_bitwise_or = "|=" ;
OPERATOR_assign_bitwise_xor = "~=" ;
OPERATOR_assign_div = "/=" ;
OPERATOR_assign_left_shift = "<<=" ;
OPERATOR_assign_mod = "%=" ;
OPERATOR_assign_mul = "*=" ;
OPERATOR_assign_or = "||=" ;
OPERATOR_assign_remainder = "%%=" ;
OPERATOR_assign_right_shift_arithmetic = ">>=" ;
OPERATOR_assign_right_shift_logical = ">>>=" ;
OPERATOR_assign_sub = "-=" ;
OPERATOR_bitwise_or = "|" ;
OPERATOR_bitwise_xor = "~" ;
OPERATOR_divide = "/" ;
OPERATOR_dereference = "^" ;
OPERATOR_equal = "==" ;
OPERATOR_greater_than_or_equal = ">=" ;
OPERATOR_left_shift = "<<" ;
OPERATOR_less_than_or_equal = "<=" ;
OPERATOR_minus = "-" ;
OPERATOR_modulus = "%" ;
OPERATOR_multiply = "*" ;
OPERATOR_not = "!" ;
OPERATOR_not_equal = "!=" ;
OPERATOR_or = "||" ;
OPERATOR_pipe = "|>" ;
OPERATOR_plus = "+" ;
OPERATOR_range_array = ".." ;
OPERATOR_range_exclusive = "..<" ;
OPERATOR_range_inclusive = "..=" ;
OPERATOR_remainder = "%%" ;
OPERATOR_right_shift_arithmetic = ">>" ;
OPERATOR_right_shift_logical = ">>>" ;

SYM_AMPERSAND = "&" ;
SYM_ARROW_SINGLE = "->" ;
SYM_ARROW_DOUBLE = "=>" ;
SYM_COLON = ":" ;
SYM_COMMA = "," ;
SYM_DOT = "." ;
SYM_ELLIPSIS = "..." ;
SYM_GT = ">" ;
SYM_LBRACE = "{" ;
SYM_LBRACK = "[" ;
SYM_LPAREN = "(" ;
SYM_LT = "<" ;
SYM_QUESTION = "?" ;
SYM_RBRACE = "}" ;
SYM_RBRACK = "]" ;
SYM_RPAREN = ")" ;
SYM_UNDERSCORE = "_" ;
SYM_UNINITIALIZED = "---" ;

# Definitions

file = package_statement , [ imports ] , [ const_definitions ] ;

package_statement = KEYWORD_package , identifier ;

imports = ( import | ( directive , [ expression_with_result ] , SYM_LBRACE , imports , SYM_RBRACE ) ) , [ imports ] ;
import = KEYWORD_import , identifier , [ KEYWORD_as , identifier ] , [ KEYWORD_from, literal_string ] ;

const_definitions = { 
    ( declaration , const_definition_rhs )
    | ( directive , SYM_LBRACE , { declaration , const_definition_rhs } , SYM_RBRACE )
    }
    ;
const_definition_rhs = SYM_COLON , decl_rhs ;
mut_definition_rhs = OPERATOR_assign , decl_rhs ;
any_definition = declaration , ( const_definition_rhs | mut_definition_rhs ) ;

declaration = identifier , SYM_COLON , [ type ]

decl_rhs = expression_with_result
    | function_decl
    | struct_decl
    | union_decl
    | enum_decl
    | ( [ KEYWORD_distinct ] , [ KEYWORD_alias ] , type )
    | SYM_UNINITIALIZED
    ;

function_decl = function_signature , block ;
struct_decl = KEYWORD_struct , [ generic_tag ] , { directive } , SYM_LBRACE , struct_body , SYM_RBRACE ;
union_decl = KEYWORD_union , [ generic_tag ] , SYM_LBRACE , union_body , SYM_RBRACE ;
enum_decl = KEYWORD_enum , SYM_LBRACE , enum_body , SYM_RBRACE ;

type = simple_type
    | complicated_type
    ;
type_in_list = simple_type
    | ( SYM_LPAREN , complicated_type , SYM_RPAREN )
    ;

simple_type = ( [ KEYWORD_mut ] , identifier , [ generic_tag ] , [ array_dimension ]+ )
    ;

# Confusing without parentheses in a function return type list
complicated_type = function_signature
    ;

array_dimension = SYM_LBRACK , [ literal_integer | OPERATOR_range_array ] , SYM_RBRACK ;

function_signature = KEYWORD_fn , [ generic_tag ] , SYM_LPAREN , [ function_parameter_list ] , SYM_RPAREN , [ SYM_ARROW , function_output_list ];
function_parameter_list = function_parameter , [ OPERATOR_assign , default_value ] , { SYM_COMMA , function_parameter , [ OPERATOR_assign , default_value ] }
    ;
function_output_list = KEYWORD_void 
    | simple_type
    | ( SYM_LPAREN , complicated_type , SYM_RPAREN )
    | ( SYM_LPAREN , function_output_in_paren , { SYM_COMMA , function_output_in_paren } , SYM_RPAREN )
    ;

function_output_in_paren = simple_type
    | ( identifier , { SYM_COMMA , identifier } , SYM_COLON , type_in_list )
    | ( SYM_LPAREN , complicated_type , SYM_RPAREN )
    ;

function_parameter = [ identifier , { SYM_COMMA , identifier } , SYM_COLON ] , type_in_list  ;

default_value = literal
    | type
    | directive
    ;

struct_body = [ struct_member , { SYM_COMMA , struct_member } ] , [ SYM_COMMA ] ;
union_body = [ union_member , { SYM_COMMA , union_member } ] , [ SYM_COMMA ] ;
enum_body = [ enum_member , { SYM_COMMA , enum_member } ] , [ SYM_COMMA ] ;

generic_tag = SYM_LBRACK , type_in_list , { SYM_COMMA , type_in_list } , SYM_RBRACK ;

struct_member = ( identifier , { SYM_COMMA , identifier } , SYM_COLON , type_in_list , [ mut_definition_rhs ] ) 
    | ( KEYWORD_using , identifier , [ SYM_DOT , identifier ] )
    | ( SYM_UNDERSCORE , SYM_COLON , struct_decl )
    ;
union_member = identifier ,  [ SYM_LPAREN , identifier , { SYM_COMMA , identifier } , SYM_RPAREN ] ;
enum_member = identifier ;

block = SYM_LBRACE , 
    { expression
    | ( KEYWORD_return , ( KEYWORD_void | ( expression_with_result , { SYM_COMMA , expression_with_result } ) ) )
    | ( KEYWORD_defer , expression )
    | ( KEYWORD_break , [ literal_integer | KEYWORD_all ] )
    | KEYWORD_continue
    | ( directive , block )
    } , SYM_RBRACE
    ;

expression = expression_with_result 
    | expression_without_result
    ;

expression_with_result = expr_lvl_1
    | block
    | match_expression
    | struct_literal
    ;

expression_without_result = for_loop
    | loop
    | push_context
    | ( declaration , [ const_definition_rhs , mut_definition_rhs ] )
    | switch_statement
    ;

literal = literal_integer
    | literal_floating_point
    | literal_string
    | literal_char
    | KEYWORD_true
    | KEYWORD_false
    | KEYWORD_null
    ;

expr_lvl_1 = expr_lvl_2 , { SYM_COMMA , expr_lvl_2 } , [ ( OPERATOR_assign
    | OPERATOR_assign_add
    | OPERATOR_assign_and
    | OPERATOR_assign_bitwise_and
    | OPERATOR_assign_bitwise_or
    | OPERATOR_assign_bitwise_xor
    | OPERATOR_assign_div
    | OPERATOR_assign_left_shift
    | OPERATOR_assign_mod
    | OPERATOR_assign_mul
    | OPERATOR_assign_or
    | OPERATOR_assign_remainder
    | OPERATOR_assign_right_shift_arithmetic
    | OPERATOR_assign_right_shift_logical
    | OPERATOR_assign_sub
    ) , expr_lvl_1 ]
    ;

expr_lvl_2 = if_expression
    | ( expr_lvl_3 , KEYWORD_is_none )
    | ( expr_lvl_3 , KEYWORD_is_some )
    | ( expr_lvl_3 , KEYWORD_or_break )
    | ( expr_lvl_3 , KEYWORD_or_continue )
    | ( expr_lvl_3 , KEYWORD_or_else , expression_with_result )
    | ( expr_lvl_3 , KEYWORD_or_return , [ expression_with_result ] )
    | expr_lvl_3
    ;

expr_lvl_3 = expr_lvl_4 , [ ( OPERATOR_range_exclusive | OPERATOR_range_inclusive ) , expr_lvl_4 ] ;

expr_lvl_4 = expr_lvl_5 , [ OPERATOR_or , expr_lvl_4 ] ;

expr_lvl_5 = expr_lvl_6 , [ OPERATOR_and , expr_lvl_5 ] ;

expr_lvl_6 = expr_lvl_7 , 
    [ ( OPERATOR_equal , expr_lvl_7 )
    | ( OPERATOR_not_equal , expr_lvl_7 )
    | ( SYM_LT , expr_lvl_7 )
    | ( SYM_GT , expr_lvl_7 )
    | ( OPERATOR_less_than_or_equal , expr_lvl_7 )
    | ( OPERATOR_greater_than_or_equal , expr_lvl_7 )
    ]
    ;

expr_lvl_7 = expr_lvl_8 ,
    [ ( OPERATOR_plus , expr_lvl_7 )
    | ( OPERATOR_minus , expr_lvl_7 )
    | ( OPERATOR_bitwise_or , expr_lvl_7 )
    | ( OPERATOR_bitwise_xor , expr_lvl_7 )
    ]
    ;

expr_lvl_8 = expr_lvl_9 , 
    [ ( OPERATOR_multiply , expr_lvl_8 )
    | ( OPERATOR_divide , expr_lvl_8 )
    | ( OPERATOR_modulus , expr_lvl_8 )
    | ( OPERATOR_remainder , expr_lvl_8 )
    | ( SYM_AMPERSAND , expr_lvl_8 )
    | ( OPERATOR_left_shift , expr_lvl_8 )
    | ( OPERATOR_right_shift_arithmetic , expr_lvl_8 )
    | ( OPERATOR_right_shift_logical , expr_lvl_8 )
    ]
    ;

expr_lvl_9 = ( SYM_LPAREN , expression_with_result , SYM_RPAREN )
    | expr_lvl_10
    ;

expr_lvl_10 = ( OPERATOR_plus , expr_lvl_11 )
    | ( OPERATOR_minus , expr_lvl_11 )
    | ( OPERATOR_bitwise_xor , expr_lvl_11 )
    | ( OPERATOR_not , expr_lvl_11 )
    | expr_lvl_11
    ;

expr_lvl_11 = literal
    | ( location , { OPERATOR_pipe , function_call } )
    | cast_expression
    | enum_shorthand
    | directive
    ;

location = { SYM_AMPERSAND } , identifier , 
    { ( SYM_LBRACK , [ expression_with_result ] , SYM_RBRACK ) 
    | ( SYM_DOT , identifier ) 
    | function_call_parens 
    | OPERATOR_dereference
    }
    ;

struct_literal = identifier , SYM_DOT , SYM_LBRACE , [ 
    ( expression_with_result , { SYM_COMMA , expression_with_result } )
    | ( identifier , OPERATOR_assign , expression_with_result , { SYM_COMMA , identifier , OPERATOR_assign , expression_with_result } , [ SYM_COMMA ] )
    ] , SYM_RBRACE
    ;

function_call_chain = function_call , { OPERATOR_pipe , function_call } ;

cast_expression = ( KEYWORD_cast , SYM_LBRACK , type , SYM_RBRACK , SYM_LPAREN , expression_with_result , SYM_RPAREN )
    | ( KEYWORD_bit_cast , SYM_LBRACK , type , SYM_RBRACK , SYM_LPAREN , expression_with_result , SYM_RPAREN )
    | ( KEYWORD_auto_cast , SYM_LPAREN , expression_with_result , SYM_RPAREN )
    ;

if_expression = KEYWORD_if , expression_with_result , block , { KEYWORD_else , KEYWORD_if , expression_with_result , block } , [ KEYWORD_else , block ] ;

for_loop = KEYWORD_for , identifier , [ SYM_COMMA , identifier ] , KEYWORD_in , expression_with_result , block ;

loop = [ KEYWORD_with , SYM_LBRACE , { any_definition } , SYM_RBRACE ] 
    , KEYWORD_loop , [ directive ] , block 
    , [ KEYWORD_while , SYM_LPAREN, expression_with_result , SYM_RPAREN ]
    ;

push_context = KEYWORD_push_context , identifier , block ;

enum_shorthand = SYM_DOT , identifier ;

function_call = identifier , function_call_parens ;

function_call_parens = SYM_LPAREN , [ function_call_input_list ] , SYM_RPAREN
    ;

function_call_input_list = [ identifier , OPERATOR_assign ] , function_call_input , { SYM_COMMA , [ identifier , OPERATOR_assign ] ,  function_call_input } 
    ;

function_call_input = expression_with_result
    | SYM_UNDERSCORE
    | pipe_reorder_identifier
    | directive
    ;

switch_statement = KEYWORD_switch , identifier , SYM_LBRACE , 
    ( switch_case , [ directive ] )+
    , SYM_RBRACE
    ;
switch_case = KEYWORD_case , switch_entry , { SYM_COMMA , switch_entry } , SYM_COLON , expression ;
switch_entry = ( literal_integer , [ (OPERATOR_range_exclusive | OPERATOR_range_inclusive) , literal_integer ] )
    | ( literal_char , [ (OPERATOR_range_exclusive | OPERATOR_range_inclusive) , literal_char ] )
    | literal_string
    | ( identifier , SYM_DOT , identifier )
    | ( enum_shorthand )
    | SYM_UNDERSCORE
    ;

match_expression = KEYWORD_match , identifier , SYM_LBRACE
    , ( match_entry , { SYM_COMMA , match_entry } , SYM_ARROW_DOUBLE , expression )+
    , SYM_RBRACE
    ;
match_entry = ( literal_integer , [ (OPERATOR_range_exclusive | OPERATOR_range_inclusive) , literal_integer ] )
    | ( literal_char , [ (OPERATOR_range_exclusive | OPERATOR_range_inclusive) , literal_char ] )
    | literal_string
    | ( identifier , SYM_DOT , identifier )
    | identifier
    | ( identifier , SYM_LPAREN , [ ( identifier | SYM_UNDERSCORE ) , { SYM_COMMA , ( identifier | SYM_UNDERSCORE ) } ] , SYM_RPAREN )
    | ( enum_shorthand )
    | ( KEYWORD_true | KEYWORD_false )
    | SYM_UNDERSCORE
    ;
